rule NgcukaClipBanker
{
	meta:
		author = "Emanuel Durmaz"
		description = "monitors the clipboard for crypto address hijacking"
		sha256 = "a01f5692ad0b55a90b8eb9a9fefa60929546ca4a3f1c99ee076caa2dda330f8a"
		sha256 = "b25d5cdf3aa2d3d4dfd491339e696311d8e912e8d14aa7809d5b95dc9b919593"
		url = "..."

    	strings:

		$n1 = "Ngcuka" ascii wide
		$n2 = "SF to CNET" ascii wide
		$n3 = "Btc Clipboard Rig" ascii wide

		$a1 = "timer1_Tick" wide ascii
		$a2 = "Form1_Load" wide ascii
		$a3 = "Clipboard" wide ascii
		$a4 = "ContainsText" wide ascii
		$a5 = "GetText" wide ascii
		$a6 = "Regex" wide ascii
		$a7 = "Replace" wide ascii
		$a8 = "SetText" wide ascii
		$a9 = "set_Interval" wide ascii

		$b1 = "/C Y /N /D Y /T 3 & Del " wide ascii
		$b2 = "HKEY_CURRENT_USER\\Software\\Microsoft\\Windows\\CurrentVersion\\Run" wide ascii
		$b3 = "ScdBcd" wide ascii
		$b4 = "([13][a-km-zA-HJ-NP-Z1-9]{25,34})" wide ascii

	condition:
		(any of ($n*)) and (7 of ($a*)) and (any of ($b*))
}
